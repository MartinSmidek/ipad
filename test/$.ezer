# ------------------------------------------------------------------------------------------------ #
# ==> Modul pro nalezení chyb v jádru Ezer                                                         #
#                                                                                                  #
# Pro práci je třeba mít uživatele Guest s prázdným heslem                                         #                                                        #
#                                                                                                  #
# Ezer                                                   (c) 2021 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #
menu test { title:'test', type:'main'
  tabs w {title:"[fa-graduation-cap] Swipe me" 
    # =======================================================================================> TOUCH
    # demonstrace ovládání dotykem
    panel touch {type:'right', title:'[fa-hand-pointer-o] Swipe test', _sys:'fce'
      menu m {type:'left', active:*, format:'f+'
        menu  {title:'Ovládání dotykem',type:'group'
          item  {title:"[fa-hand-pointer-o] Browse", par:{cmd:'on'}}
          func onclick(i:ezer) { var lorem:text
            clear(); info.header(i);
            lorem= php.lorem_ipsum(10); info.swipe.browse_fill(replace(lorem,' ','|'));
          }
        }
      }
      use info: form right {
        label [10,40,200,20] { title:'<b>test swipe</b>' }
        browse swipe [10,60,200,200] { rows:20, qry_rows:1
          show [,,200,] { title:'lorem ipsum' }
        }
      }
    }
    # ===================================================================================== APLIKACE
    panel APPL {type:'right', title:'[fa-download] Nastavení', _sys:'str' // skill:'m'
      menu m {type:'left', format:'f+'
        menu { title:'GitHub', type:'group', active:no, //skill:'m'
          item { title:'[fa-question] show log'         par:{op:'show'}}
          // aplikace
          item { title:'[fa-question] Aplikace: git status'  par:{folder:'.',op:'cmd',cmd:'status'}}
          item { title:'[fa-gear] Aplikace: git log'         par:{folder:'.',op:'cmd',cmd:'log'}}
          item { title:'[fa-gear] Aplikace: git pull'        par:{folder:'.',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gear] Aplikace: git fetch ...'   par:{folder:'.',op:'cmd',cmd:'fetch'}
                                                                help:"po fetch>git reset --hard origin/master"}
          // jádro
          item { title:'[fa-question] Ezer: git status'  par:{folder:'ezer',op:'cmd',cmd:'status'}}
          item { title:'[fa-gears] Ezer: git log'        par:{folder:'ezer',op:'cmd',cmd:'log'}}
          item { title:'[fa-gears] Ezer: git pull'       par:{folder:'ezer',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gears] Ezer: git fetch ...'  par:{folder:'ezer',op:'cmd',cmd:'fetch'}
                                                            help:"po fetch>git reset --hard origin/ezer3.2"}
          func onclick (i:ezer) {  
            info.header(i);
            // test zabraňující běhu na NTB
            if (sys('ezer','server')==0) 
              alert('nelze na vývojovém serveru')  
            else
              info.fill('',php.git_make(i.par));
          }
        }
      }
      use info: form right 
    }
  }
}
# ------------------------------------------------==> .. _right
# univerzální formulář pro levostranné menu
form right [,,*,50] {
  label head [10,0,*,50]  { title:'' }
  label note [10,50,*,550] { title:'' }
  func header(i:ezer) { var i_owner:ezer
    clear(); i_owner= i.owner();
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
  func fill(x,y) {
    if (x) head= `<div class='karta'>${replace_fa(x)}</div>`;
    if (y) note= y
  }
  func append(y) {
    if (y) note= conc(note,y)
  }
}
